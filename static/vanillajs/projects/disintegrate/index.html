<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Document</title>

    <style>
        body {
            margin: 0;
        }
        #c { 
            width: 100%;
            height: 450px;
        }
    </style>
</head>
<body>
    <script id="vertexShader" type="NOT_JS">
        varying vec2 vUv;

        void main() {
            vUv = uv;
         
            gl_Position = vec4(position, 1.0);
        }
    </script>
    <script id="fragmentShader" type="NOT_JS">
        varying vec2 vUv;
        uniform vec2 uResolution;
        uniform float uTime;
 
        // noise stuff
        float random (in vec2 st) {
            return fract(sin(dot(st.xy,
                                 vec2(12.9898,78.233)))*
                43758.5453123);
        }
        
        // Based on Morgan McGuire @morgan3d
        // https://www.shadertoy.com/view/4dS3Wd
        float noise (in vec2 st) {
            vec2 i = floor(st);
            vec2 f = fract(st);
        
            // Four corners in 2D of a tile
            float a = random(i);
            float b = random(i + vec2(1.0, 0.0));
            float c = random(i + vec2(0.0, 1.0));
            float d = random(i + vec2(1.0, 1.0));
        
            vec2 u = f * f * (3.0 - 2.0 * f);
        
            return mix(a, b, u.x) +
                    (c - a)* u.y * (1.0 - u.x) +
                    (d - b) * u.x * u.y;
        }
        
        #define OCTAVES 6
        float fbm (in vec2 st) {
            // Initial values
            float value = 0.0;
            float amplitude = .5;
            float frequency = 0.;
            //
            // Loop of octaves
            for (int i = 0; i < OCTAVES; i++) {
                value += amplitude * noise(st);
                st *= 2.;
                amplitude *= .5;
            }
            return value;
        }

        // SDF stuff
        float sdfSphere(vec3 p, float r) {
            return length( p ) - r;
        }

        // the total 'map' of the world
        float sdf (vec3 p) { 
            return sdfSphere(p, 0.8) + 0.25 * sin(uTime * 0.3) * clamp(fbm(p.xy / 2.), 0., 1.);
        }

        float shadow( in vec3 ro, in vec3 rd, float mint, float maxt )
        {
            for( float t=mint; t<maxt; )
            {
                float h = sdf(ro + rd*t);
                if( h<0.001 )
                    return 0.0;
                t += h;
            }
            return 1.0;
        }

        vec3 calcNormal( in vec3 p ) // for function f(p)
        {
            const float eps = 0.0001; // or some other value
            const vec2 h = vec2(eps,0);
            return normalize( vec3(sdf(p+h.xyy) - sdf(p-h.xyy),
                                sdf(p+h.yxy) - sdf(p-h.yxy),
                                sdf(p+h.yyx) - sdf(p-h.yyx) ) );
        }

        void main() {
            vec3 pos = vec3( 0, 0, -3.0);
            float aspect = uResolution.x / uResolution.y;
            vec3 dir = normalize( vec3( vec2(aspect, 1.0) * (vUv - vec2(0.5)), 1.0 ) );

            vec3 ip; // intersection point
        
            float t = 0.0;
            float tMax = 10.0;
            float eps = 0.0001;
            for( int i = 0; i < 256; i++) {
                ip = pos + dir * t;
        
                float temp = sdf( ip );

                if( temp < eps || t > tMax) break;

                t += temp;
            }

            vec3 color = vec3(0.0);
            if (t < tMax - eps) { 
                vec3 normal = calcNormal(ip);
                //vec3 lightPosition = vec3(4,0,-10);
                vec3 lightPosition = vec3( (1.0 + 30.0 * abs(sin(uTime * 0.5))) * cos(uTime), 
                                           (2.0 + 30.5 * abs(cos(uTime * 0.5))) * sin(uTime),
                                           -5);
                vec3 lightPosition2 = lightPosition * vec3(-1,-1,1);
                float lightOneColor = 0.9 * shadow(ip, normalize(lightPosition-ip), 0.1, tMax);
                float lightTwoColor = 0.7 * shadow(ip, normalize(lightPosition2-ip), 0.1, tMax);
                color = vec3( lightOneColor - lightTwoColor) + 0.4 * sin(uTime * 0.1) * 0.28 * normal.brg;
                //color = clamp(vec3(dot(lightPosition, normal)), 0., 1.) - vec3(0.05);
            }
    
            gl_FragColor = vec4(color.rgb, 1.0);
        }
    </script>
    <canvas id="c"></canvas>

    <script src="index.js" type="module"></script>
</body>
</html>